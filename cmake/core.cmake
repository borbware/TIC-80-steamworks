################################
# TIC-80 core
################################

if(WIN32)
    add_library(dlfcn STATIC ${THIRDPARTY_DIR}/dlfcn/src/dlfcn.c)

    target_include_directories(dlfcn
        INTERFACE
            ${THIRDPARTY_DIR}/dirent/include
            ${THIRDPARTY_DIR}/dlfcn/src)
endif()

set(BUILD_DEPRECATED TRUE)

set(TIC80CORE_DIR ${CMAKE_SOURCE_DIR}/src)
set(TIC80CORE_SRC
    ${TIC80CORE_DIR}/fftdata.c
    ${TIC80CORE_DIR}/core/core.c
    ${TIC80CORE_DIR}/core/draw.c
    ${TIC80CORE_DIR}/core/io.c
    ${TIC80CORE_DIR}/core/sound.c
    ${TIC80CORE_DIR}/tic.c
    ${TIC80CORE_DIR}/cart.c
    ${TIC80CORE_DIR}/tools.c
    ${TIC80CORE_DIR}/zip.c
    ${TIC80CORE_DIR}/tilesheet.c
    ${TIC80CORE_DIR}/script.c
    ${TIC80CORE_DIR}/ext/fft.c
    ${TIC80CORE_DIR}/ext/kiss_fft.c
    ${TIC80CORE_DIR}/ext/kiss_fftr.c
    ${TIC80CORE_DIR}/ext/png.c
)

if(BUILD_DEPRECATED)
    set(TIC80CORE_SRC ${TIC80CORE_SRC} ${TIC80CORE_DIR}/ext/gif.c)
endif()

if(BUILD_WITH_STEAM)
    set(TIC80CORE_SRC ${TIC80CORE_SRC}
        ${TIC80CORE_DIR}/ext/csteamapi.c
        ${TIC80CORE_DIR}/ext/steamapi.cpp
)
endif()

add_library(tic80core STATIC ${TIC80CORE_SRC})

if (FREEBSD)
    target_include_directories(tic80core PRIVATE ${SYSROOT_PATH}/usr/local/include)
    target_link_directories(tic80core PRIVATE ${SYSROOT_PATH}/usr/local/lib)
endif()

if(WIN32)
    target_link_libraries(tic80core PUBLIC dlfcn)
endif()

target_include_directories(tic80core
    PRIVATE
        ${THIRDPARTY_DIR}/moonscript
        ${THIRDPARTY_DIR}/fennel
        ${THIRDPARTY_DIR}/yuescript
        ${POCKETPY_DIR}/src
    PUBLIC
        ${CMAKE_SOURCE_DIR}/include
        ${CMAKE_SOURCE_DIR}/src)

target_link_libraries(tic80core PRIVATE png)
target_link_libraries(tic80core PRIVATE blipbuf)

if(BUILD_WITH_ZLIB)
    target_link_libraries(tic80core PRIVATE zlib)
endif()

if(BUILD_WITH_STEAM)
    target_include_directories(tic80core
        PRIVATE
            ${CMAKE_SOURCE_DIR}/include/steam
            ${CMAKE_SOURCE_DIR}/include/redistributable_bin)
    if(WIN32)
        if(CMAKE_SIZEOF_VOID_P EQUAL 8)
            target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/steam/lib/win64/sdkencryptedappticket64.lib)
            target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/redistributable_bin/win64/steam_api64.lib)
        else()
            target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/steam/lib/win32/sdkencryptedappticket.lib)
            target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/redistributable_bin/steam_api.lib)
        endif()
    endif()
    if(APPLE)
        target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/steam/lib/osx/libsdkencryptedappticket.dylib)
        target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/redistributable_bin/osx/libsteam_api.dylib)
    endif()
    if(LINUX)
        target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/steam/lib/linux64/libsdkencryptedappticket.so)
        target_link_libraries(tic80core PRIVATE ${CMAKE_SOURCE_DIR}/include/redistributable_bin/linux64/steam_api.so)
    endif()
endif()

if(BUILD_STATIC)
    if(BUILD_WITH_LUA)
        target_link_libraries(tic80core PRIVATE lua)
    endif()

    if(BUILD_WITH_MOON)
        target_link_libraries(tic80core PRIVATE moon)
    endif()

    if(BUILD_WITH_YUE)
        target_link_libraries(tic80core PRIVATE yuescript)
    endif()

    if(BUILD_WITH_FENNEL)
        target_link_libraries(tic80core PRIVATE fennel)
    endif()

    if(BUILD_WITH_JS)
        target_link_libraries(tic80core PRIVATE js)
    endif()

    if(BUILD_WITH_SCHEME)
        target_link_libraries(tic80core PRIVATE scheme)
    endif()

    if(BUILD_WITH_SQUIRREL)
        target_link_libraries(tic80core PRIVATE squirrel)
    endif()

    if(BUILD_WITH_PYTHON)
        target_link_libraries(tic80core PRIVATE python)
    endif()

    if(BUILD_WITH_WREN)
        target_link_libraries(tic80core PRIVATE wren)
    endif()

    if(BUILD_WITH_RUBY)
        target_link_libraries(tic80core PRIVATE ruby)
    endif()

    if(BUILD_WITH_JANET)
        target_link_libraries(tic80core PRIVATE janet)
    endif()

    if(BUILD_WITH_WASM)
        target_link_libraries(tic80core PRIVATE wasm)
    endif()

    target_link_libraries(tic80core PRIVATE runtime)

endif()

if(BUILD_DEPRECATED)
    target_compile_definitions(tic80core PRIVATE BUILD_DEPRECATED)
    target_link_libraries(tic80core PRIVATE giflib)
endif()

if(LINUX)
    target_link_libraries(tic80core PRIVATE m dl)
endif()
